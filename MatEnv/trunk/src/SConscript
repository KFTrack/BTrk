#
# $Id:$
# $Author:$
# $Date:$
#
# Original author Rob Kutschke.
#

import os, re
Import('env')

plugin_cc = Glob('*_plugin.cc', strings=True)
non_plugin_cc = Glob('*.cc', strings=True)
for cc in plugin_cc: non_plugin_cc.remove(cc)

if non_plugin_cc:
    # From: /path1/path2/path3/Name/src extract the string Name field.
    dd=re.search('[^/]*/[^/]*$',env.Dir('.').abspath).group(0)
    local_lib_name='BaBar_'+os.path.dirname(dd)
    env.SharedLibrary('#/lib/'+local_lib_name, non_plugin_cc,
                      LIBS=[ 'art_Framework_Core',
                             'GeometryService',
                             'Mu2eUtilities',
                             'GeneralUtilities',
                             'CLHEP',
                           ],
                      )
    libs=[ local_lib_name ]
else:
    libs=[]

for cc in plugin_cc:
    libname='#/lib/'+cc[:cc.find('.cc')]
    env.SharedLibrary( libname, cc,
                     LIBS=[ 'art_Framework_Core', libs, 'ToyDP', 'GeometryService',
                            'GeneralUtilities', 'CLHEP',
                          ],
                     )

# This tells emacs to view this file in python mode.
# Local Variables:
# mode:python
# End:
